cmake_minimum_required (VERSION 3.10)
project (ndarray VERSION 0.0.1 LANGUAGES C CXX)

include(GNUInstallDirs)
# include (ExternalProject)

option (NDARRAY_BUILD_TESTS "Build tests" OFF)
option (NDARRAY_BUILD_EXAMPLES "Build examples" OFF)

set (CMAKE_EXPORT_COMPILE_COMMANDS ON)
set (CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake)
set (EXECUTABLE_OUTPUT_PATH ${PROJECT_BINARY_DIR}/bin)
set (LIBRARY_OUTPUT_PATH ${PROJECT_BINARY_DIR}/lib)

# dependencies
function (ndarray_option name description default)
  set (NDARRAY_USE_${name} ${default} CACHE STRING "${description}")
  set_property (CACHE NDARRAY_USE_${name} PROPERTY STRINGS "TRUE;AUTO;FALSE")
endfunction ()

ndarray_option (ADIOS2 "Use ADIOS2" FALSE)
ndarray_option (CUDA "Use CUDA" FALSE) # experimental
ndarray_option (HIP "Use HIP" FALSE) # experimental, AMD GPUs
ndarray_option (IntelSYCL "Use IntelSYCL" FALSE) # experimental, cross-platform acceleration
ndarray_option (EIGEN "Use Eigen" FALSE) # Eigen backend for conversion/interop
ndarray_option (HDF5 "Use HDF5" FALSE)
ndarray_option (HENSON "Use HENSON" FALSE) # experimental
ndarray_option (MPI "Use MPI" FALSE)
ndarray_option (NETCDF "Use NetCDF" FALSE)
ndarray_option (OpenMP "Use OpenMP" FALSE)
ndarray_option (PNETCDF "Use parallel-netcdf" FALSE)
ndarray_option (PNG "Use PNG" FALSE)
ndarray_option (VTK "Use VTK" FALSE)
ndarray_option (XTENSOR "Use xtensor" FALSE) # xtensor backend for conversion/interop
ndarray_option (YAML "Use YAML (for stream functionality)" AUTO) # yaml-cpp for YAML streams

set (CMAKE_CXX_STANDARD 17)
set (CMAKE_CXX_STANDARD_REQUIRED ON)

if (CMAKE_CXX_BYTE_ORDER STREQUAL LITTLE_ENDIAN)
  set (NDARRAY_USE_LITTLE_ENDIAN TRUE)
else ()
  set (NDARRAY_USE_BIG_ENDIAN TRUE)
endif ()

find_package (Threads REQUIRED)

if (NDARRAY_USE_YAML STREQUAL AUTO)
  find_package (yaml-cpp QUIET)
elseif (NDARRAY_USE_YAML)
  find_package (yaml-cpp REQUIRED)
endif ()
if (yaml-cpp_FOUND)
  set (NDARRAY_HAVE_YAML ON)
endif ()

if (NDARRAY_USE_ADIOS2 STREQUAL AUTO)
  find_package (ADIOS2 QUIET)
elseif (NDARRAY_USE_ADIOS2)
  find_package (ADIOS2 REQUIRED)
endif ()
if (ADIOS2_FOUND)
  if (ADIOS2_HAVE_MPI)
    set (NDARRAY_USE_MPI ON)
  endif ()
  set (NDARRAY_HAVE_ADIOS2 ON)
  include_directories (${ADIOS2_INCLUDE_DIRS})
  # Capture version for config file (ensure minimum version compatibility)
  if (NOT DEFINED ADIOS2_VERSION_MAJOR)
    set (ADIOS2_VERSION_MAJOR 2)
  endif ()
  if (NOT DEFINED ADIOS2_VERSION_MINOR)
    set (ADIOS2_VERSION_MINOR 0)
  endif ()
endif ()

if (NDARRAY_USE_VTK STREQUAL AUTO)
  find_package (VTK QUIET)
elseif (NDARRAY_USE_VTK)
  find_package (VTK REQUIRED)
endif ()
if (VTK_FOUND)
  set (NDARRAY_HAVE_VTK TRUE)
  if (VTK_MAJOR_VERSION STRLESS 9) # vtk8.9 does not require VTK_USE_FILE as well
    include (${VTK_USE_FILE})
  else ()
    # VTK 9+ uses target-based includes, but we need global includes for headers
    # Get include directories from VTK::CommonCore target
    get_target_property(VTK_INCLUDE_DIRS VTK::CommonCore INTERFACE_INCLUDE_DIRECTORIES)
    include_directories (${VTK_INCLUDE_DIRS})
  endif ()
endif ()

# NetCDF detection - support both CMake and autotools builds
if (NDARRAY_USE_NETCDF STREQUAL AUTO)
  # Try CMake config first (for CMake-built netcdf-c)
  find_package (netCDF QUIET)

  # Fallback to pkg-config (for autotools-built netcdf-c)
  if (NOT netCDF_FOUND AND PKG_CONFIG_FOUND)
    pkg_check_modules(NETCDF QUIET netcdf)
    if (NETCDF_FOUND)
      set (netCDF_FOUND TRUE)
      set (netCDF_INCLUDE_DIR ${NETCDF_INCLUDE_DIRS})
      set (netCDF_LIBRARIES ${NETCDF_LIBRARIES})
    endif()
  endif()

  # Fallback to nc-config script (for autotools-built netcdf-c without pkg-config)
  if (NOT netCDF_FOUND)
    find_program(NC_CONFIG nc-config)
    if (NC_CONFIG)
      execute_process(COMMAND ${NC_CONFIG} --includedir OUTPUT_VARIABLE NC_INCLUDE_DIR OUTPUT_STRIP_TRAILING_WHITESPACE)
      execute_process(COMMAND ${NC_CONFIG} --libs OUTPUT_VARIABLE NC_LIBS OUTPUT_STRIP_TRAILING_WHITESPACE)
      if (EXISTS "${NC_INCLUDE_DIR}/netcdf.h")
        set (netCDF_FOUND TRUE)
        set (netCDF_INCLUDE_DIR ${NC_INCLUDE_DIR})
        # Extract library path and names from nc-config --libs output
        string(REGEX MATCHALL "-L([^ ]+)" NC_LIB_DIRS "${NC_LIBS}")
        string(REGEX REPLACE "-L" "" netCDF_LIBRARY_DIRS "${NC_LIB_DIRS}")
        string(REGEX MATCHALL "-l([^ ]+)" NC_LIB_NAMES "${NC_LIBS}")
        string(REGEX REPLACE "-l" "" netCDF_LIBRARIES "${NC_LIB_NAMES}")
      endif()
    endif()
  endif()
elseif (NDARRAY_USE_NETCDF)
  # Try CMake config first (for CMake-built netcdf-c)
  find_package (netCDF QUIET)

  # Fallback to pkg-config (for autotools-built netcdf-c)
  if (NOT netCDF_FOUND AND PKG_CONFIG_FOUND)
    pkg_check_modules(NETCDF REQUIRED netcdf)
    set (netCDF_FOUND TRUE)
    set (netCDF_INCLUDE_DIR ${NETCDF_INCLUDE_DIRS})
    set (netCDF_LIBRARIES ${NETCDF_LIBRARIES})
  endif()

  # Fallback to nc-config script (for autotools-built netcdf-c without pkg-config)
  if (NOT netCDF_FOUND)
    find_program(NC_CONFIG nc-config)
    if (NC_CONFIG)
      execute_process(COMMAND ${NC_CONFIG} --includedir OUTPUT_VARIABLE NC_INCLUDE_DIR OUTPUT_STRIP_TRAILING_WHITESPACE)
      execute_process(COMMAND ${NC_CONFIG} --libs OUTPUT_VARIABLE NC_LIBS OUTPUT_STRIP_TRAILING_WHITESPACE)
      if (EXISTS "${NC_INCLUDE_DIR}/netcdf.h")
        set (netCDF_FOUND TRUE)
        set (netCDF_INCLUDE_DIR ${NC_INCLUDE_DIR})
        # Extract library path and names from nc-config --libs output
        string(REGEX MATCHALL "-L([^ ]+)" NC_LIB_DIRS "${NC_LIBS}")
        string(REGEX REPLACE "-L" "" netCDF_LIBRARY_DIRS "${NC_LIB_DIRS}")
        string(REGEX MATCHALL "-l([^ ]+)" NC_LIB_NAMES "${NC_LIBS}")
        string(REGEX REPLACE "-l" "" netCDF_LIBRARIES "${NC_LIB_NAMES}")
      else()
        message(FATAL_ERROR "NetCDF not found. Tried CMake config, pkg-config, and nc-config.")
      endif()
    else()
      message(FATAL_ERROR "NetCDF not found. Tried CMake config, pkg-config, and nc-config script not found.")
    endif()
  endif()
endif ()

if (netCDF_FOUND)
  set (NDARRAY_HAVE_NETCDF TRUE)
  include_directories (${netCDF_INCLUDE_DIR})

  # Check for HDF5 and parallel support if available
  if (netCDF_HAS_HDF5)
    set (NDARRAY_USE_HDF5 ON)
  endif ()
  if (netCDF_HAS_PARALLEL)
    set (NDARRAY_USE_MPI AUTO)
    set (NDARRAY_HAVE_NETCDF_PARALLEL TRUE)
  endif ()

  # For autotools builds, try to detect parallel support from nc-config
  if (NC_CONFIG AND NOT DEFINED netCDF_HAS_PARALLEL)
    execute_process(COMMAND ${NC_CONFIG} --has-parallel OUTPUT_VARIABLE NC_HAS_PARALLEL OUTPUT_STRIP_TRAILING_WHITESPACE)
    if (NC_HAS_PARALLEL STREQUAL "yes")
      set (NDARRAY_USE_MPI AUTO)
      set (NDARRAY_HAVE_NETCDF_PARALLEL TRUE)
    endif()
  endif()
endif ()


if (NDARRAY_USE_MPI STREQUAL AUTO)
  find_package (MPI QUIET COMPONENTS MPICXX CXX)
elseif (NDARRAY_USE_MPI)
  find_package (MPI REQUIRED COMPONENTS MPICXX CXX)
else ()
  # MPI explicitly disabled - prevent find_package from being called
  set (MPI_FOUND FALSE)
endif ()

if (MPI_FOUND)
  set (NDARRAY_HAVE_MPI TRUE)
  set (CMAKE_CXX_COMPILER ${MPI_CXX_COMPILER})
  include_directories (${MPI_C_INCLUDE_DIRS})
else ()
  set (NDARRAY_HAVE_MPI FALSE)
endif ()


if (NDARRAY_USE_HDF5 STREQUAL AUTO)
  find_package (HDF5 QUIET)
elseif (NDARRAY_USE_HDF5)
  find_package (HDF5 REQUIRED)
endif()
if (HDF5_FOUND)
  set (NDARRAY_HAVE_HDF5 TRUE)
  include_directories (${HDF5_INCLUDE_DIRS})

  # Capture version for config file (ensure minimum version compatibility)
  if (HDF5_VERSION)
    string(REGEX MATCH "^([0-9]+)\\.([0-9]+)" HDF5_VERSION_MATCH "${HDF5_VERSION}")
    set (HDF5_VERSION_MAJOR ${CMAKE_MATCH_1})
    set (HDF5_VERSION_MINOR ${CMAKE_MATCH_2})
  else ()
    # Fallback if version not detected
    set (HDF5_VERSION_MAJOR 1)
    set (HDF5_VERSION_MINOR 10)
  endif ()

  # Check for parallel HDF5 support
  if (HDF5_IS_PARALLEL)
    set (NDARRAY_HAVE_PARALLEL_HDF5 TRUE)
    message(STATUS "HDF5 has parallel support (H5_HAVE_PARALLEL)")
  else()
    set (NDARRAY_HAVE_PARALLEL_HDF5 FALSE)
    message(STATUS "HDF5 does NOT have parallel support")
    message(STATUS "  To enable: rebuild HDF5 with --enable-parallel and MPI")
  endif()
endif ()


# Henson
if (NDARRAY_USE_HENSON STREQUAL AUTO)
  # do nothing
elseif (NDARRAY_USE_HENSON)
  if                          (NOT DEFINED HENSON AND DEFINED ENV{HENSON})
      set                     (HENSON $ENV{HENSON})
  endif                       ()

  if                          (HENSON)
      set                     (HENSON_LIBRARY ${HENSON}/lib/libhenson.a)
      set                     (HENSON_PMPI_LIBRARY ${HENSON}/lib/libhenson-pmpi.so)
  else                        ()
      message                 ("Looking for Henson")
      find_package            (henson REQUIRED)
  endif                       ()
  message                     ("HENSON_LIBRARY = ${HENSON_LIBRARY}")
  message                     ("HENSON_PMPI_LIBRARY = ${HENSON_PMPI_LIBRARY}")
  # set                         (libraries ${libraries} ${HENSON_PMPI_LIBRARY} ${HENSON_LIBRARY})
endif ()


find_package(PkgConfig)
if (NDARRAY_USE_PNETCDF STREQUAL AUTO)
  if (PKG_CONFIG_FOUND)
    pkg_check_modules(PNETCDF QUIET pnetcdf)
  endif()
  # Fallback to manual detection if pkgconfig not found
  if (NOT PNETCDF_FOUND AND DEFINED ENV{PNETCDF_DIR})
    set(PNETCDF_INCLUDE_DIRS "$ENV{PNETCDF_DIR}/include")
    set(PNETCDF_LIBRARY_DIRS "$ENV{PNETCDF_DIR}/lib")
    find_library(PNETCDF_LIBRARIES NAMES pnetcdf PATHS ${PNETCDF_LIBRARY_DIRS} NO_DEFAULT_PATH)
    if (EXISTS "${PNETCDF_INCLUDE_DIRS}/pnetcdf.h" AND PNETCDF_LIBRARIES)
      set(PNETCDF_FOUND TRUE)
    endif()
  endif()
elseif (NDARRAY_USE_PNETCDF)
  if (PKG_CONFIG_FOUND)
    pkg_check_modules(PNETCDF REQUIRED pnetcdf)
  else()
    # Manual detection required when pkgconfig not available
    if (DEFINED ENV{PNETCDF_DIR})
      set(PNETCDF_INCLUDE_DIRS "$ENV{PNETCDF_DIR}/include")
      set(PNETCDF_LIBRARY_DIRS "$ENV{PNETCDF_DIR}/lib")
      find_library(PNETCDF_LIBRARIES NAMES pnetcdf PATHS ${PNETCDF_LIBRARY_DIRS} NO_DEFAULT_PATH)
      if (EXISTS "${PNETCDF_INCLUDE_DIRS}/pnetcdf.h" AND PNETCDF_LIBRARIES)
        set(PNETCDF_FOUND TRUE)
      else()
        message(FATAL_ERROR "PNetCDF not found. Set PNETCDF_DIR environment variable.")
      endif()
    else()
      message(FATAL_ERROR "PkgConfig not found and PNETCDF_DIR not set. Either install pkg-config or set PNETCDF_DIR environment variable.")
    endif()
  endif()
endif()
if (PNETCDF_FOUND)
  set (NDARRAY_HAVE_PNETCDF TRUE)
  include_directories (${PNETCDF_INCLUDE_DIRS})
endif ()

if (NDARRAY_USE_PNG STREQUAL AUTO)
  find_package (PNG QUIET)
elseif (NDARRAY_USE_PNG)
  find_package (PNG REQUIRED)
endif ()
if (PNG_FOUND)
  set (NDARRAY_HAVE_PNG TRUE)
  include_directories (${PNG_INCLUDE_DIRS})
endif ()

if (NDARRAY_USE_OpenMP STREQUAL AUTO)
  find_package (OpenMP QUIET)
elseif (NDARRAY_USE_OpenMP)
  find_package (OpenMP REQUIRED)
endif ()
if (OpenMP_CXX_FOUND)
  set (NDARRAY_HAVE_OPENMP TRUE)
endif ()

if (NDARRAY_USE_CUDA STREQUAL AUTO)
  enable_language (CUDA OPTIONAL)
  find_package (CUDAToolkit OPTIONAL)
  if (CMAKE_CUDA_COMPILER)
    set (NDARRAY_HAVE_CUDA TRUE)
  endif ()
elseif (NDARRAY_USE_CUDA)
  enable_language (CUDA)
  find_package (CUDAToolkit)
  set (NDARRAY_HAVE_CUDA TRUE)
endif ()

if (NDARRAY_USE_HIP STREQUAL AUTO)
  find_package (HIP QUIET)
elseif (NDARRAY_USE_HIP)
  find_package (HIP REQUIRED)
endif ()
if (HIP_FOUND)
  set (NDARRAY_HAVE_HIP TRUE)
endif ()

if (NDARRAY_USE_IntelSYCL STREQUAL AUTO)
  find_package (IntelSYCL QUIET)
elseif (NDARRAY_USE_IntelSYCL)
  find_package (IntelSYCL REQUIRED)
endif ()
if (IntelSYCL_FOUND)
  set (NDARRAY_HAVE_SYCL TRUE)
  include_directories (${SYCL_INCLUDE_DIR})
endif ()

if (NDARRAY_USE_EIGEN STREQUAL AUTO)
  find_package (Eigen3 QUIET NO_MODULE)
elseif (NDARRAY_USE_EIGEN)
  find_package (Eigen3 REQUIRED NO_MODULE)
else ()
  # Explicitly disabled
  set (NDARRAY_HAVE_EIGEN FALSE)
endif ()
if (Eigen3_FOUND OR EIGEN3_FOUND)
  set (NDARRAY_HAVE_EIGEN TRUE)
  if (TARGET Eigen3::Eigen)
    # Modern CMake target
    set (EIGEN3_INCLUDE_DIR ${EIGEN3_INCLUDE_DIR})
  endif ()
  if (EIGEN3_INCLUDE_DIR)
    include_directories (${EIGEN3_INCLUDE_DIR})
  endif ()
endif ()

if (NDARRAY_USE_XTENSOR STREQUAL AUTO)
  find_package (xtensor QUIET)
elseif (NDARRAY_USE_XTENSOR)
  find_package (xtensor REQUIRED)
else ()
  # Explicitly disabled
  set (NDARRAY_HAVE_XTENSOR FALSE)
endif ()
if (xtensor_FOUND)
  set (NDARRAY_HAVE_XTENSOR TRUE)
  message(STATUS "xtensor found - enabling xtensor storage backend")
endif ()


###

set (NDARRAY_INCLUDE_DIR
  ${CMAKE_CURRENT_SOURCE_DIR}/include
  ${CMAKE_CURRENT_BINARY_DIR}/include
)

include_directories (${NDARRAY_INCLUDE_DIR})

# Add xtensor include directories after basic includes
if (NDARRAY_HAVE_XTENSOR)
  if (TARGET xtensor)
    get_target_property(XTENSOR_INCLUDE_DIRS xtensor INTERFACE_INCLUDE_DIRECTORIES)
    if (XTENSOR_INCLUDE_DIRS)
      include_directories (${XTENSOR_INCLUDE_DIRS})
      message(STATUS "Added xtensor include dirs: ${XTENSOR_INCLUDE_DIRS}")
    endif ()
  endif ()
  if (TARGET xtl)
    get_target_property(XTL_INCLUDE_DIRS xtl INTERFACE_INCLUDE_DIRECTORIES)
    if (XTL_INCLUDE_DIRS)
      include_directories (${XTL_INCLUDE_DIRS})
      message(STATUS "Added xtl include dirs: ${XTL_INCLUDE_DIRS}")
    endif ()
  endif ()
endif ()

configure_file(${CMAKE_CURRENT_SOURCE_DIR}/include/ndarray/config.hh.in
  ${CMAKE_CURRENT_BINARY_DIR}/include/ndarray/config.hh)

install (DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/include/ndarray DESTINATION
  ${CMAKE_INSTALL_INCLUDEDIR})
install (FILES ${CMAKE_CURRENT_BINARY_DIR}/include/ndarray/config.hh
  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/ndarray)

configure_file(${CMAKE_CURRENT_SOURCE_DIR}/cmake/ndarrayConfig.cmake.in
  ${CMAKE_CURRENT_BINARY_DIR}/ndarrayConfig.cmake)

include (CMakePackageConfigHelpers)
set (NDARRAY_ROOT_DIR ${CMAKE_INSTALL_PREFIX})
set (NDARRAY_INCLUDE_DIR ${CMAKE_INSTALL_PREFIX}/include)
configure_package_config_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/cmake/ndarrayConfig.cmake.in
  ${CMAKE_CURRENT_BINARY_DIR}/ndarrayConfig.cmake
  PATH_VARS NDARRAY_INCLUDE_DIR
  INSTALL_DESTINATION ${CMAKE_INSTALL_PREFIX}/cmake/ndarray
  NO_CHECK_REQUIRED_COMPONENTS_MACRO)
install (FILES ${CMAKE_CURRENT_BINARY_DIR}/ndarrayConfig.cmake
  DESTINATION ${CMAKE_INSTALL_PREFIX}/lib/cmake/ndarray/)


add_subdirectory (src)

# If NDARRAY_BUILD_EXAMPLES is on, build examples.
if (NDARRAY_BUILD_EXAMPLES)
  add_subdirectory (examples)
endif ()

# If NDARRAY_BUILD_TESTS is on, enable testing.
if (NDARRAY_BUILD_TESTS)
  if (NDARRAY_HAVE_MPI)
    # set (TEST_EXECUTOR ${MPIEXEC_EXECUTABLE})
  endif()

  enable_testing ()
  include (Catch)
  add_subdirectory (tests)
endif ()


## summary
message ("")
message ("NDARRAY build configuration:")
message ("  NDARRAY Version:  ${NDARRAY_VERSION}")
message ("")
message ("  C++ compiler: ${CMAKE_CXX_COMPILER_ID} ${CMAKE_CXX_COMPILER_VERSION} ${CMAKE_CXX_COMPILER_WRAPPER}")
message ("                ${CMAKE_CXX_COMPILER}")
message ("")
message ("  Installation prefix: ${CMAKE_INSTALL_PREFIX}")
message("        bin: ${CMAKE_INSTALL_BINDIR}")
message("        lib: ${CMAKE_INSTALL_LIBDIR}")
message("    include: ${CMAKE_INSTALL_INCLUDEDIR}")
message("      cmake: ${CMAKE_INSTALL_CMAKEDIR}")
message ("")
message("  Features:")
message("    Build Type:   ${CMAKE_BUILD_TYPE}")
message("    Executables:  ${NDARRAY_BUILD_EXECUTABLES}")
message("    Applications: ${NDARRAY_BUILD_APPLICATIONS}")
message("    Testing:      ${NDARRAY_BUILD_TESTS}")
message ("")
message("  Dependencies:")
message("    ADIOS2       ${NDARRAY_USE_ADIOS2} ${ADIOS2_FOUND}")
if (${NDARRAY_HAVE_ADIOS2})
  message("      Include path:    ${ADIOS2_INCLUDES}")
  message("      Libraries:       ${ADIOS2_LIBRARIES}")
endif ()
message("    Boost:       ${NDARRAY_USE_Boost} ${Boost_FOUND}")
if (${NDARRAY_HAVE_BOOST})
  message("      Version:         ${Boost_VERSION_STRING}")
  message("      Include path:    ${Boost_INCLUDE_DIR}")
  message("      Libraries:       ${Boost_LIBRARIES}")
endif ()
message("    CGAL:        ${NDARRAY_USE_CGAL} ${CGAL_FOUND}")
if (${NDARRAY_HAVE_CGAL})
  message("      CGAL path:       ${CUDA_VERSION}")
endif ()
message("    CUDA:        ${NDARRAY_USE_CUDA}")
if (${NDARRAY_HAVE_CUDA})
  message("      Host compiler:      ${CMAKE_CUDA_HOST_COMPILER}")
  message("      NVCC compiler:      ${CMAKE_CUDA_COMPILER}")
  message("      NVCC flags:         ${CMAKE_CUDA_FLAGS}")
  message("      Detected GPU count: ${DETECT_GPU_COUNT}")
endif()
message("    IntelSYCL:   ${NDARRAY_USE_IntelSYCL}")
if (${NDARRAY_HAVE_SYCL})
  message("      SYCL implementation: ${SYCL_IMPLEMENTATION_ID}")
  message("      SYCL include dir:    ${SYCL_INCLUDE_DIR}")
  message("      SYCL compiler flags: ${SYCL_FLAGS}")
endif()
message("    HDF5:        ${NDARRAY_USE_HDF5} ${HDF5_FOUND}")
if (${NDARRAY_HAVE_HDF5})
  message("      Version:           ${HDF5_VERSION}")
  message("      Include path:      ${HDF5_INCLUDE_DIRS}")
  message("      Definitions:       ${HDF5_DEFINITIONS}")
  message("      Libraries:         ${HDF5_LIBRARIES}")
  if (NDARRAY_HAVE_PARALLEL_HDF5)
    message("      Parallel support:  YES")
  else()
    message("      Parallel support:  NO (rebuild HDF5 with --enable-parallel for parallel I/O)")
  endif()
endif ()
message("    HENSON:      ${NDARRAY_USE_HENSON}")
if (${NDARRAY_USE_HENSON})
  message("      Libraries:         ${HENSON_PMPI_LIBRARY} ${HENSON_LIBRARY}")
endif ()
message("    MPI:         ${NDARRAY_USE_MPI} ${MPI_FOUND}")
if (${NDARRAY_HAVE_MPI})
  message("      MPI C++ compiler:  ${MPI_CXX_COMPILER}")
  message("      mpiexec:           ${MPIEXEC_EXECUTABLE}")
endif ()
message("    MPSolve:     ${NDARRAY_USE_MPSolve} ${MPSolve_FOUND}")
message("    netCDF:      ${NDARRAY_USE_NETCDF} ${NETCDF_FOUND}")
if (${NDARRAY_HAVE_NETCDF})
  message("      Version:         ${NetCDFVersion}")
  message("      Path:            ${netCDF_INSTALL_PREFIX}")
endif ()
message("    OpenMP*:     ${NDARRAY_USE_OpenMP} ${OpenMP_CXX_FOUND}")
if (${NDARRAY_HAVE_OPENMP})
  message("      Version:         ${OpenMP_CXX_VERSION}")
  message("      Flags:           ${OpenMP_CXX_FLAGS}")
  message("      Library:         ${OpenMP_CXX_LIBRARY}")
endif ()
message("    PNETCDF*:    ${NDARRAY_USE_PNETCDF} ${PNETCDF_FOUND}")
if (${NDARRAY_HAVE_PNETCDF})
  message("      Include path:   ${PNETCDF_INCLUDE_DIRS}")
  message("      Libraries:      ${PNETCDF_LIBRARIES}")
endif ()
message("    PNG:         ${NDARRAY_USE_PNG} ${PNG_FOUND}")
if (${NDARRAY_HAVE_PNG})
  message("      Library:   ${PNG_LIBRARY}")
endif()
message("    VTK:         ${NDARRAY_USE_VTK} ${NDARRAY_HAVE_VTK}")
if (${NDARRAY_HAVE_VTK})
  message("      VTK_DIR:     ${VTK_DIR}")
  message("      VTK_VERSION: ${VTK_MAJOR_VERSION}.${VTK_MINOR_VERSION}")
endif ()
message ("")
message ("  (*) Experimental and not recommend to use")
message ("")
