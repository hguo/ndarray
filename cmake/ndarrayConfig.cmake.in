# get_filename_component(NDARRAY_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
include("${CMAKE_CURRENT_LIST_DIR}/ndarrayTargets.cmake")

list (INSERT CMAKE_MODULE_PATH 0 "${CMAKE_CURRENT_LIST_DIR}")

set (NDARRAY_HAVE_ADIOS2 @NDARRAY_HAVE_ADIOS2@)
set (NDARRAY_HAVE_CUDA @NDARRAY_HAVE_CUDA@)
set (NDARRAY_HAVE_GMP @NDARRAY_HAVE_GMP@)
set (NDARRAY_HAVE_HDF5 @NDARRAY_HAVE_HDF5@)
set (NDARRAY_HAVE_METIS @NDARRAY_HAVE_METIS@)
set (NDARRAY_HAVE_MPI @NDARRAY_HAVE_MPI@)
set (NDARRAY_HAVE_NETCDF @NDARRAY_HAVE_NETCDF@)
set (NDARRAY_HAVE_PNETCDF @NDARRAY_HAVE_PNETCDF@)
set (NDARRAY_HAVE_VTK @NDARRAY_HAVE_VTK@)
set (NDARRAY_HAVE_YAML @NDARRAY_HAVE_YAML@)

if (NDARRAY_HAVE_YAML)
  find_dependency (yaml-cpp REQUIRED)
endif ()

if (NDARRAY_HAVE_ADIOS2)
  # Require at least the version ndarray was built with to avoid ABI incompatibilities
  set (_ADIOS2_VERSION "@ADIOS2_VERSION_MAJOR@.@ADIOS2_VERSION_MINOR@")
  if (_ADIOS2_VERSION MATCHES "^[0-9]+\\.[0-9]+")
    find_dependency (ADIOS2 ${_ADIOS2_VERSION} REQUIRED)
  else ()
    find_dependency (ADIOS2 REQUIRED)
  endif ()
  unset (_ADIOS2_VERSION)
endif ()

if (NDARRAY_HAVE_CUDA)
  enable_language (CUDA)
  find_dependency (CUDAToolkit REQUIRED)
endif ()

if (NDARRAY_HAVE_VTK)
  set (_VTK_VERSION "@VTK_MAJOR_VERSION@.@VTK_MINOR_VERSION@")
  if (_VTK_VERSION MATCHES "^[0-9]+\\.[0-9]+")
    find_dependency (VTK ${_VTK_VERSION} REQUIRED)
  else ()
    find_dependency (VTK REQUIRED)
  endif ()
  unset (_VTK_VERSION)
endif ()

if (NDARRAY_HAVE_METIS)
  find_dependency (METIS REQUIRED)
endif ()

if (NDARRAY_HAVE_MPI)
  find_dependency (MPI REQUIRED)
  include_directories (${MPI_C_INCLUDE_PATH})
endif ()

if (NDARRAY_HAVE_HDF5)
  # Require at least the version ndarray was built with to avoid ABI incompatibilities
  set (_HDF5_VERSION "@HDF5_VERSION_MAJOR@.@HDF5_VERSION_MINOR@")
  if (_HDF5_VERSION MATCHES "^[0-9]+\\.[0-9]+")
    find_dependency (HDF5 ${_HDF5_VERSION} REQUIRED)
  else ()
    find_dependency (HDF5 REQUIRED)
  endif ()
  unset (_HDF5_VERSION)
  include_directories (${HDF5_INCLUDE_DIRS})
endif ()

if (NDARRAY_HAVE_NETCDF)
  find_dependency (netCDF REQUIRED)
  include_directories (${netCDF_INCLUDE_DIR})
endif ()

if (NDARRAY_HAVE_PNETCDF)
  # PNetCDF typically uses pkg-config
  find_package (PkgConfig QUIET)
  if (PKG_CONFIG_FOUND)
    pkg_check_modules (PNETCDF REQUIRED pnetcdf)
    include_directories (${PNETCDF_INCLUDE_DIRS})
  else ()
    message (WARNING "PNetCDF was enabled but pkg-config not found. Set PNETCDF_INCLUDE_DIRS and PNETCDF_LIBRARIES manually.")
  endif ()
endif ()

set (NDARRAY_INCLUDE_DIR "@NDARRAY_INCLUDE_DIR@")
include_directories (${NDARRAY_INCLUDE_DIR})

set (NDARRAY_FOUND 1)
set (NDARRAY_LIBRARY "@NDARRAY_LIBRARY@")
